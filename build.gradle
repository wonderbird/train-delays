plugins {
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.2'
}

group = 'systems.boos'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Generate OpenAPI documentation from Javadoc, see https://springdoc.org/#javadoc-support
    annotationProcessor 'com.github.therapi:therapi-runtime-javadoc-scribe:0.15.0'
    implementation 'com.github.therapi:therapi-runtime-javadoc:0.15.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.4.0'

    // adding the Jackson reference automatically allows processing application/xml response bodies
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.14.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.3'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: "com.vaadin.external.google", module: "android-json"
    }
    testImplementation 'net.sourceforge.htmlunit:htmlunit:2.70.0'

    testImplementation 'au.com.dius.pact.consumer:java8:4.1.42'
    testImplementation 'au.com.dius.pact.consumer:junit5:4.6.9'
    testImplementation 'au.com.dius.pact.provider:junit5spring:4.6.7'

    testImplementation 'org.json:json:20240303'
    testImplementation 'org.apache.commons:commons-collections4:4.4'
    testImplementation 'io.cucumber:cucumber-spring:7.16.1'
    testImplementation 'io.cucumber:cucumber-java:7.16.1'
    testImplementation 'io.cucumber:cucumber-junit:7.16.1'
    testImplementation 'org.mock-server:mockserver-netty:5.15.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            mainClass = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'systems.boos.traindelays', 'src/test/resources']
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true // coveralls plugin depends on xml format report
        html.required = true
    }
}

// For coveralls to work in the GitHub action, you need to configure the COVERALLS_REPO_TOKEN secret in the
// GitHub project settings.
coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}
