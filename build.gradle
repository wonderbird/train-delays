plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'java'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.0'
}

group = 'systems.boos'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    // adding the Jackson reference automatically allows processing application/xml response bodies
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.3'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: "com.vaadin.external.google", module: "android-json"
    }
    testImplementation 'net.sourceforge.htmlunit:htmlunit:2.62.0'

    testImplementation 'au.com.dius.pact.consumer:java8:4.1.38'
    testImplementation 'au.com.dius.pact.consumer:junit5:4.3.12'
    testImplementation 'au.com.dius.pact.provider:junit5spring:4.3.12'

    testImplementation 'org.json:json:20220320'
    testImplementation 'org.apache.commons:commons-collections4:4.4'
    testImplementation 'io.cucumber:cucumber-spring:7.5.0'
    testImplementation 'io.cucumber:cucumber-java:7.4.1'
    testImplementation 'io.cucumber:cucumber-junit:7.5.0'
    testImplementation 'org.mock-server:mockserver-netty:5.13.2'
    testImplementation 'org.mockito:mockito-inline:4.6.1'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            mainClass = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'systems.boos.traindelays', 'src/test/resources']
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true // coveralls plugin depends on xml format report
        html.required = true
    }
}

// For coveralls to work in the GitHub action, you need to configure the COVERALLS_REPO_TOKEN secret in the
// GitHub project settings.
coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}
